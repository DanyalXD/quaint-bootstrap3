
require:
   path
   quaint -> etools ->
      enode-to-data
      trim
   quaint-nav

var gid = 0

special = new Set with {
   ".default"
   ".primary"
   ".success"
   ".info"
   ".warning"
   ".danger"

   ".dismissible"

   ".lg"
   ".sm"
}

root = path.join{__dirname, "..", "bootstrap-3.3.6-dist/"}
swatch = path.join{__dirname, "..", "bootswatch/"}

enhance{tags, cls, t = 'div'} =
   {t, '.{cls}'} ++
      tags each match tag ->
         when special.has{tag} ->
            '.{cls}-{tag[1..]}'
         else ->
            tag

enhancer{cls, t = 'div'}{tags, props, children} =
   ENode{enhance{tags, cls, t}, props, children}


_dropdown{direction, tags, props, {title, enode-to-data! list}} =
   div %
      class = direction
      button.btn.btn-default.dropdown-toggle %
         type = .button
         "data-toggle" = .dropdown
         "aria-haspopup" = "true"
         "aria-expanded" = "true"
         title
         span.caret %
      ul.dropdown-menu %
         list each match element ->
            {R"^[ \n]*$"?} ->
               li.divider %
            else ->
               li % element

_tabs{type, tags, props, children} =
   {tabs-map >> enode-to-data{tabs-map, true}} =
      children each child when not R"[ \n]+"? child ->
         child
   tabs = enumerate{consume! tabs-map.entries{}} each {i, {title, body}} ->
      {i, title, body, '__btab__{gid += 1}'}
   {
      ul.nav %
         class = 'nav-{type}'
         tabs each {i, title, _, id} ->
            li %
               class = if{i === 0, .active, ""}
               a %
                  "data-toggle" = .tab
                  href = '#{id}'
                  title
      .tab-content %
         class = 'from-{type}'
         tabs each {i, _, contents, id} ->
            div.tab-pane %
               class = if{i === 0, .active, ""}
               id = id
               contents
   }


default-template = """
doctype :: html
html %
  head %
    title %
      meta::title !! Untitled
    meta %
      http-equiv = Content-type
      content = text/html
      charset = UTF-8
    meta %
      name = viewport
      content = width=device-width, initial-scale=1
  body % {body}
"""

nav-template = """
template :: @minimal
bsnav :: dump!
container % {body}
"""

defaults = {
   nav-container-class = "container-fluid"
}

install{@, _options} =
   options = defaults & _options

   qn-options = {
      ulonly = true
      wrap-dropdown{title} =
         a.dropdown-toggle %
            href = "#"
            "data-toggle" = .dropdown
            role = .button
            "aria-haspopup" = .true
            "aria-expanded" = .false
            title
            span.caret %
      wrap-element{match elem} =
         [a %]? or {"", [a %]?} ->
            elem
         else ->
            p.navbar-text % elem
   }
   quaint-nav{@, qn-options}

   @register-resolvers with {
      template = {
         "@minimal" => default-template
         "@bootstrap" => nav-template
         "@bootstrap3" => nav-template
      }
   }

   @register-macros with {
      brand{engine, body} =
         engine.macros.store{engine, "bootstrap-brand", body}

      bsnav{engine, body} =
         if body.raw{}.trim{} !== "dump!":
            throw E.quaint-bootstrap with
               'bsnav:: macro can only have "dump!" as its body'
         nav-id = "the-collapsible-nav"
         nav.navbar.navbar-default %
            div %
               class = options.nav-container-class
               .navbar-header %
                  button.navbar-toggle.collapsed %
                     type = .button
                     "data-toggle" = .collapse
                     "data-target" = '#{nav-id}'
                     "aria-expanded" = .false
                     span.sr-only % "Toggle navigation"
                     span.icon-bar %
                     span.icon-bar %
                     span.icon-bar %
                  a.navbar-brand %
                     engine.macros.dump{engine, "bootstrap-brand"}
               .collapse.navbar-collapse %
                  id = nav-id
                  inherit.nav.navbar-nav.navbar-left %
                     quaint-nav.dump-nav{@, "left", qn-options}
                  inherit.nav.navbar-nav %
                     quaint-nav.dump-nav{@, "main", qn-options}
                  inherit.nav.navbar-nav.navbar-right %
                     quaint-nav.dump-nav{@, "right", qn-options}
   }

   @register-resources with {
      "bootstrap/css/bootstrap.css" => {
         path =
            match options.theme as f:
               undefined? or "@default" ->
                  path.join{root, "css/bootstrap.min.css"}
               R"^@(.*)"! {_, name} ->
                  path.join{swatch, '{name}/bootstrap.min.css'}
               else ->
                  ;; TODO: resolve f at the proper moment, when options given
                  f
         contents = true
         type = .css
         method = .head
         dependencies = {
            "bootstrap/fonts" => #copy{path.join{root, "fonts/"}}
         }
      }
      "jquery.js" => {
         path = path.join{__dirname, "..", "jquery-1.11.3.min.js"}
         contents = true
         type = .js
         method = .head
      }
      "bootstrap/js/bootstrap.js" => {
         path = path.join{root, "js/bootstrap.min.js"}
         contents = true
         type = .js
         method = .head
      }
   }

   @register-components with {
      ;; TODO
      ;; badge, breadcrumbs
      ;; embed, jumbotron
      ;; list-group, media
      primary{tags, props, children} =
         @components.alert{{".primary"} ++ tags, props, children}
      success{tags, props, children} =
         @components.alert{{".success"} ++ tags, props, children}
      info{tags, props, children} =
         @components.alert{{".info"} ++ tags, props, children}
      warning{tags, props, children} =
         @components.alert{{".warning"} ++ tags, props, children}
      danger{tags, props, children} =
         @components.alert{{".danger"} ++ tags, props, children}

      alert{tags, props, children} =
         ENode with
            enhance{tags, .alert}
            props
            {
               if ".dismissible" in tags:
                  then:
                     button.close %
                        type = .button
                        "data-dismiss" = .alert
                        raw % "&times;"
                  else: {}
               children
            }

      container = enhancer{.container}

      divider = enhancer{.divider}

      dropdown{tags, props, children} =
         _dropdown{.dropdown, tags, props, children}

      dropup{tags, props, children} =
         _dropdown{.dropup, tags, props, children}

      glyph{tags, props, children} =
         {name >> name.trim{}} = children
         span %
            class = 'glyphicon glyphicon-{name}'

      label = enhancer{.label, .span}

      ;; ;; TODO: IMPERFECT
      ;; nav{tags, props, {*brand, enode-to-data! list}} =
      ;;    id = '__bnav__{gid += 1}'
      ;;    nav.navbar.navbar-default %
      ;;       .container-fluid %
      ;;          .navbar-header %
      ;;             button.navbar-toggle.collapsed %
      ;;                type = .button
      ;;                "data-toggle" = .collapse
      ;;                "data-target" = '#{id}'
      ;;                "aria-expanded" = .false
      ;;                span.icon-bar %
      ;;                span.icon-bar %
      ;;                span.icon-bar %
      ;;             match trim{brand}:
      ;;                [a %]? {=> tags, => props, => children} ->
      ;;                   ENode{tags ++ {".navbar-brand"}, props, children}
      ;;                else ->
      ;;                   .navbar-brand % brand
      ;;          .collapse.navbar-collapse %
      ;;             id = id
      ;;             ul.nav.navbar-nav %
      ;;                list each entry ->
      ;;                   li % entry

      panel{tags, props, children} =
         {title, *body} = children each
            child when not String? child or child.trim{} != "" ->
               child
         ENode with
            enhance{tags, .panel}
            props
            {
               div.panel-heading %
                  match title:
                     ENode? {=> tags, => props, => children} ->
                        ENode{{*tags, ".panel-title"}, props, children}
                     else -> title
               div.panel-body %
                  body
            }

      pills{tags, props, children} =
         _tabs{.pills, tags, props, children}

      table{tags, props, children} =
         ENode{{'table', '.table', *tags}, props, children}

      tabs{tags, props, children} =
         _tabs{.tabs, tags, props, children}

      well = enhancer{.well}
   }

   @register-rules with {
      "!! \\glyph"{engine, {=> glyph}} =
         glyph % glyph.raw{}
   }


main{*match} =
   {@, options = {=}} when @is-quaint-engine ->
      install{@, options}
   {options = {=}} ->
      {@} -> main{@, options}

provide = main

